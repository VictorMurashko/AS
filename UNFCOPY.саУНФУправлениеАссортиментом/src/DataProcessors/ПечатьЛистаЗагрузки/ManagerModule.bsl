

Функция ПредставлениеПФ()  Экспорт
		Возврат НСтр("ru = 'Лист загрузки'");		
КонецФункции

Функция КлючПараметровПечати()  Экспорт
	Возврат "ПАРАМЕТРЫ_ПЕЧАТИ_ЛистЗагрузки";
КонецФункции

Функция ПолныйПутьКМакету() Экспорт
	Возврат "Обработка.ПечатьЛистаЗагрузки.ПФ_MXL_ЛистСборки";
КонецФункции

Функция ИдентификаторПечатнойФормы() Экспорт
	
	Возврат  "ЛистЗагрузки";
	
КонецФункции

Функция ДанныеЛистаЗагрузки(МассивОбъектов)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказПокупателя.Контрагент КАК ПредставлениеРеципиента,
		|	ЗаказПокупателя.АдресДоставки КАК АдресДоставки,
		|	ЗаказПокупателя.ПАК_МаршрутныйЛист.Номер КАК МаршрутныйЛистНомер,
		|	ЗаказПокупателя.Ссылка КАК Ссылка,
		|	ЗаказПокупателя.Номер КАК Номер,
		|	ЗаказПокупателя.Дата КАК Дата,
		|	ЗаказПокупателя.ФИОВодителя КАК Водитель,
		|	ЗаказПокупателя.ДатаОтгрузки КАК ДатаОтгрузки,
		|	ЗаказПокупателя.ПАК_МаршрутныйЛист.Автомобиль КАК Автомобиль,
		|	ЗаказПокупателя.ПАК_МаршрутныйЛист.ЗонаТариф КАК ЗонаТариф,
		|	ЗаказПокупателя.ПАК_МаршрутныйЛист.ДатаЗагрузки КАК ДатаЗагрузки,
		|	ВЫРАЗИТЬ(ЗаказПокупателя.Комментарий КАК СТРОКА(1000)) КАК Комментарий,
		|	""Заказ"" КАК ИмяДокумента
		|ИЗ
		|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
		|ГДЕ
		|	ЗаказПокупателя.Ссылка В(&МассивОбъектов)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПеремещениеЗапасов.СтруктурнаяЕдиницаПолучатель,
		|	ПеремещениеЗапасов.АдресДоставки,
		|	ПеремещениеЗапасов.ПАК_МаршрутныйЛист.Номер,
		|	ПеремещениеЗапасов.Ссылка,
		|	ПеремещениеЗапасов.Номер,
		|	ПеремещениеЗапасов.Дата,
		|	ПеремещениеЗапасов.ФИОВодителя,
		|	ПеремещениеЗапасов.ДатаОтгрузки,
		|	ПеремещениеЗапасов.ПАК_МаршрутныйЛист.Автомобиль,
		|	ПеремещениеЗапасов.ПАК_МаршрутныйЛист.ЗонаТариф,
		|	ПеремещениеЗапасов.ПАК_МаршрутныйЛист.ДатаЗагрузки,
		|	ВЫРАЗИТЬ(ПеремещениеЗапасов.Комментарий КАК СТРОКА(1000)),
		|	""Перемещение""
		|ИЗ
		|	Документ.ПеремещениеЗапасов КАК ПеремещениеЗапасов
		|ГДЕ
		|	ПеремещениеЗапасов.Ссылка В(&МассивОбъектов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	АдресДоставки,
		|	ПредставлениеРеципиента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказПокупателяЗапасы.Номенклатура КАК Номенклатура,
		|	ЗаказПокупателяЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ЗаказПокупателяЗапасы.Количество КАК Количество,
		|	ЗаказПокупателяЗапасы.Вес КАК Вес,
		|	Номенклатура2.КатегорияНоменклатуры КАК КатегорияНоменклатуры,
		|	ЗаказПокупателяЗапасы.Ссылка КАК Ссылка,
		|	ЗаказПокупателяЗапасы.Ссылка.Контрагент.Представление КАК Контрагент
		|ИЗ
		|	Документ.ЗаказПокупателя.Запасы КАК ЗаказПокупателяЗапасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура2
		|		ПО ЗаказПокупателяЗапасы.Номенклатура = Номенклатура2.Ссылка
		|ГДЕ
		|	ЗаказПокупателяЗапасы.Ссылка В(&МассивОбъектов)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПеремещениеЗапасов.Номенклатура,
		|	ПеремещениеЗапасов.ЕдиницаИзмерения,
		|	ПеремещениеЗапасов.Количество,
		|	&Вес,
		|	Номенклатура2.КатегорияНоменклатуры,
		|	ПеремещениеЗапасов.Ссылка,
		|	ПеремещениеЗапасов.Ссылка.СтруктурнаяЕдиницаПолучатель.Представление
		|ИЗ
		|	Документ.ПеремещениеЗапасов.Запасы КАК ПеремещениеЗапасов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура2
		|		ПО ПеремещениеЗапасов.Номенклатура = Номенклатура2.Ссылка
		|ГДЕ
		|	ПеремещениеЗапасов.Ссылка В(&МассивОбъектов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Контрагент";
	
	
	Запрос.Текст=СтрЗаменить(Запрос.Текст,"&Вес","ПеремещениеЗапасов.Вес");
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Возврат  РезультатЗапроса;
	

КонецФункции // ()
 
Функция ПолучитьФИОКладовщика()
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СотрудникиПользователя.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ ВТ_СОТР
		|ИЗ
		|	РегистрСведений.СотрудникиПользователя КАК СотрудникиПользователя
		|ГДЕ
		|	СотрудникиПользователя.Пользователь = &Пользователь
		|	И НЕ СотрудникиПользователя.Сотрудник = ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ВТ_СОТР.Сотрудник.Физлицо.Представление, """") КАК ФИО
		|ПОМЕСТИТЬ ВТ_ФЛ
		|ИЗ
		|	ВТ_СОТР КАК ВТ_СОТР
		|ГДЕ
		|	НЕ ВТ_СОТР.Сотрудник.Физлицо = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВТ_ФЛ.ФИО КАК ФИО
		|ИЗ
		|	ВТ_ФЛ КАК ВТ_ФЛ";
	
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
	        Возврат "";
	Иначе	
	      	Выборка = Результат.Выбрать();
          	Выборка.Следующий();
		 	Возврат ФизическиеЛицаКлиентСервер.ФамилияИнициалы(Выборка.ФИО);

	КонецЕсли; 
	
КонецФункции // ()
 

Функция СформироватьПФ(ПечатнаяФорма, МассивОбъектов, ОбъектыПечати)   Экспорт
	ФИОКладовщика =  ПолучитьФИОКладовщика();
	ТабличныйДокумент = ПечатнаяФорма.ТабличныйДокумент;
	
		
		ПолныйПутьКМакету	=	ПечатнаяФорма.ПолныйПутьКМакету;
		ПервыйДокумент = Истина;
		
		Макет = УправлениеПечатью.МакетПечатнойФормы(ПолныйПутьКМакету);
		ДанныеЛистаЗагрузки =  ДанныеЛистаЗагрузки(МассивОбъектов);
		ВыборкаДокументов = ДанныеЛистаЗагрузки[0].Выбрать();
		ТабличнаяЧасть = ДанныеЛистаЗагрузки[1].Выгрузить();
				
		СтруктураПараметров = Новый Структура;
		Пока ВыборкаДокументов.Следующий() Цикл
			ТекущийДокумент = ВыборкаДокументов.Ссылка;
			
			Если НЕ ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаДокументов.Номер, Истина, Истина);
			
			ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
			СтруктураПараметров.Очистить();
			Шаблон = НСтр("ru = 'Лист загрузки по документу %1 №%2 от %3'");
			СтруктураПараметров.Вставить("ТекстЗаголовка", СтрШаблон(Шаблон, ВыборкаДокументов.ИмяДокумента, НомерДокумента,Формат(ВыборкаДокументов.ДатаОтгрузки, "ДЛФ=DD")));
			ОбластьМакета.Параметры.Заполнить(СтруктураПараметров);
			ОбластьМакета.Параметры.Водитель =  ВыборкаДокументов.Водитель;
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			СтруктураПараметров.Очистить();
			ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
			ОбластьМакета.Параметры.Заполнить(ВыборкаДокументов);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			
			ОбластьМакета = Макет.ПолучитьОбласть("Строка");
			НомерСтроки=0;  ВсегоВес=0; ВсегоКоличество=0;
			СтруктураПоиска = Новый Структура("Ссылка", ВыборкаДокументов.Ссылка);
			МассивВыборкаТабличныхЧ=ТабличнаяЧасть.НайтиСтроки(СтруктураПоиска);
			Для каждого ВыборкаТабличныхЧ Из МассивВыборкаТабличныхЧ Цикл
				    НомерСтроки=НомерСтроки+1;
					ВсегоВес=ВсегоВес+ВыборкаТабличныхЧ.Вес;
					ВсегоКоличество=ВсегоКоличество+ВыборкаТабличныхЧ.Количество;

					КодНоменклатуры =   ВыборкаТабличныхЧ.Номенклатура.Код;
					Попытка
					      КодНоменклатурыЧ = ЧИСЛО(ПРАВ(КодНоменклатуры,8));
					Исключение
					      КодНоменклатурыЧ = КодНоменклатуры;
					КонецПопытки;
					
										
					ОбластьМакета.Параметры.Заполнить(ВыборкаТабличныхЧ);
					ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
					ОбластьМакета.Параметры.Код = КодНоменклатурыЧ;
					ТабличныйДокумент.Вывести(ОбластьМакета);
			КонецЦикла; 
			

			ОбластьМакета = Макет.ПолучитьОбласть("Итого");
			ОбластьМакета.Параметры.ВсегоВес = ВсегоВес;
			ОбластьМакета.Параметры.ВсегоКоличество = ВсегоКоличество;
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("Ответственный");
			Ответственный	=	"";
			Если ТипЗнч(ТекущийДокумент)=Тип("ДокументСсылка.ПеремещениеЗапасов") Тогда
			    ОбластьМакета.Параметры.Ответственный= ТекущийДокумент.Автор.Наименование;
			ИначеЕсли ТипЗнч(ТекущийДокумент)=Тип("ДокументСсылка.ЗаказПокупателя") Тогда
	             ОбластьМакета.Параметры.Ответственный= ТекущийДокумент.Ответственный.Наименование;
			КонецЕсли; 
			ТабличныйДокумент.Вывести(ОбластьМакета);
			//ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
			//ОбластьМакета.Параметры.ФИОКладовщика = ФИОКладовщика;
			//ТабличныйДокумент.Вывести(ОбластьМакета);

			КонецЦикла;
		
	
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	Возврат ТабличныйДокумент;
	
КонецФункции



